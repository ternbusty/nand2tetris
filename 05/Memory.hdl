// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // デマルチプレキサ (4 way) を使えばもっと簡潔に書ける
    // address[13] = address[14] = 1 ならキーボード (ifkey0 = 1)
    And(a=address[13], b=address[14], out=ifkey0);
    // address[13] = 0, address[14] = 1 ならスクリーン (ifsc0 = 1)
    Not(in=address[13], out=not13);
    And(a=not13, b=address[14], out=ifsc0);
    // address[14] = 0 なら RAM (ifram0 = 1)
    DMux(in=true, sel=address[14], a=ifram0, b=tmp);

    And(a=ifram0, b=load, out=ifram);
    And(a=ifsc0, b=load, out=ifsc);

    RAM16K(in=in, load=ifram, address=address[0..13], out=outram);
    Screen(in=in, load=ifsc, address=address[0..12], out=outsc);
    Keyboard(out=outkey);

    // マルチプレキサ (4way) を使えばもっと簡潔に書ける
    // address[13] = 0 ならスクリーン、1 ならキーボード
    Mux16(a=outsc, b=outkey, sel=address[13], out=scorkey);
    // address[14] = 0 なら RAM、1 ならスクリーンかキーボードのどちらか
    Mux16(a=outram, b=scorkey, sel=address[14], out=out);
}